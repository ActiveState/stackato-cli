# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2014 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## link-user-org <user> [org]
## --auditor --billing --manager
## developer implied, always

kt check Tcl     8.5
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt source support/common.tcl

# # ## ### ##### ######## ############# #####################

test link-user-org-1.0 {link-user-org, wrong args, not enough} -constraints cfv2 -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run link-user-org -n
} -cleanup {
    stage-close
} -returnCodes error -match glob -result "wrong#args, not enough*"

test link-user-org-1.1 {link-user-org, wrong args, too many} -constraints cfv2 -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run link-user-org U O X
} -cleanup {
    remove-appdir
    stage-close
} -returnCodes error -match glob -result "wrong#args, too many*"

test link-user-org-1.2 {link-user-org, not allowed for cfv1} -constraints cfv1 -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run link-user-org [theorg]-2
} -cleanup {
    stage-close
} -returnCodes error -result {Error: This command requires a target exporting the CF v2 API}

test link-user-org-1.3 {link-user-org, login required} -constraints cfv2 -setup {
    stage-open
    ref-target
} -body {
    run link-user-org [theuser] [theorg]-2
} -cleanup {
    stage-close
} -returnCodes error -result [login-required]

test link-user-org-1.4 {link-user-org, non-admin login is not ok} -constraints cfv2 -setup {
    stage-open
    go-admin
    go-non-admin
} -body {
    run link-user-org [theuser] [theorg]-2
} -cleanup {
    be-admin
    remove-non-admin
    stage-close
} -returnCodes error -result [unexpected input user {A user} [theuser]]

test link-user-org-1.5 {link-user-org, persists} -constraints cfv2 -setup {
    stage-open
    go-admin
    run create-org -n [theorg]-2 --no-activate
    go-non-admin
    be-admin
} -body {
    run link-user-org [theuser] [theorg]-2
    run users --mode related
} -cleanup {
    run unlink-user-org [theuser] [theorg]-2
    run delete-org   -n [theorg]-2
    remove-non-admin
    stage-close
} -match glob -result "* [theuser] *| ---D [theorg]-2 *|*"

foreach {n bits options} {
    0 A--D --auditor
    1 -B-D --billing
    2 --MD --manager
    3 AB-D {--billing --auditor}
    4 -BMD {--manager --billing}
    5 A-MD {--manager --auditor}
    6 ABMD {--manager --billing --auditor}
} {
    test link-user-org-1.6.$n "link-user-org $options " -constraints cfv2 -setup {
	stage-open
	go-admin
	run create-org -n [theorg]-2 --no-activate
	go-non-admin
	be-admin
    } -body {
	run link-user-org [theuser] [theorg]-2 {*}$options
	run users --mode related
    } -cleanup {
	run unlink-user-org [theuser] [theorg]-2 {*}$options
	run delete-org   -n [theorg]-2
	remove-non-admin
	stage-close
    } -match glob -result "* [theuser] *| $bits [theorg]-2 *|*"
}

# # ## ### ##### ######## ############# #####################
cleanupTests
