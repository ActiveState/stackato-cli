# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2014 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## quota create

kt check Tcl     8.5
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt source support/common.tcl

# # ## ### ##### ######## ############# #####################

test quota-create-1.0 {quota create, wrong args, too many} -setup {
    stage-open
} -body {
    run quota create Q X
} -cleanup {
    stage-close
} -returnCodes error -match glob -result "wrong#args, too many*"

test quota-create-1.1 {quota create, wrong args, not enough} -constraints cfv2 -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run quota create -n
} -cleanup {
    stage-close
} -returnCodes error -result {Missing definition for argument 'name'}

test quota-create-1.2 {quota requires CFv2 API} -constraints cfv1 -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run quota create -n Q
} -cleanup {
    stage-close
} -returnCodes error -result {Error: This command requires a target exporting the CF v2 API}

test quota-create-1.3 {quota create, not logged in} -constraints cfv2 -setup {
    stage-open
    ref-target
} -body {
    run quota create testquota
} -cleanup {
    stage-close
} -returnCodes error -result [login-required]

test quota-create-1.4 {quota create, non-admin login} -constraints cfv2 -setup {
    stage-open
    ref-target
    go-admin
    go-non-admin
} -body {
    run quota create testquota
} -cleanup {
    go-admin
    remove-non-admin
    stage-close
} -returnCodes error -result [not-authorized]

test quota-create-1.5 {quota create, defaults} -constraints cfv2 -setup {
    stage-open
    go-admin
} -body {
    run quota create testquota
} -cleanup {
    run quota delete -n testquota
    stage-close
} -result [viewFile data/qc15.txt]

test quota-create-1.6 {quota create, persists} -constraints cfv2 -setup {
    stage-open
    go-admin
} -body {
    run quota create testquota
    run quota show   testquota
} -cleanup {
    run quota delete -n testquota
    stage-close
} -match glob -result [map [viewFile data/qc16.txt] \
			   @target@ [thetarget]]

test quota-create-1.7 {quota create, non-standard configuration} -constraints cfv2 -setup {
    stage-open
    go-admin
} -body {
    run quota create testquota \
	--allow-sudo \
	--paid-services-allowed \
	--trial-db-allowed \
	--routes 400 \
	--services 300 \
	--mem 200
    run quota show testquota
} -cleanup {
    run quota delete -n testquota
    stage-close
} -match glob -result [map [viewFile data/qc17.txt] \
			   @target@ [thetarget]]

# # ## ### ##### ######## ############# #####################
cleanupTests
