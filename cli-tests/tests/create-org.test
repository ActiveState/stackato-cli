# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2014 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## create-org

kt check Tcl     8.5
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt source support/common.tcl

# # ## ### ##### ######## ############# #####################

test create-org-1.0 {create-org, wrong args, not enough} -constraints cfv2 -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run create-org -n
} -cleanup {
    stage-close
} -returnCodes error -result {Missing definition for argument 'name'}

test create-org-1.1 {create-org, wrong args, too many} -constraints cfv2 -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run create-org S X
} -cleanup {
    remove-appdir
    stage-close
} -returnCodes error -match glob -result "wrong#args, too many*"

test create-org-1.2 {create-org, not allowed for cfv1} -constraints cfv1 -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run create-org [theorg]-2
} -cleanup {
    stage-close
} -returnCodes error -result {Error: This command requires a target exporting the CF v2 API}

test create-org-1.3 {create-org, login required} -constraints cfv2 -setup {
    stage-open
    ref-target
} -body {
    run create-org [theorg]-2
} -cleanup {
    stage-close
} -returnCodes error -result [login-required]

test create-org-1.4.0 {create-org, non-admin login is not ok} -constraints s234le -setup {
    stage-open
    go-admin
    go-non-admin
} -body {
    run create-org [theorg]-2
} -cleanup {
    be-admin
    remove-non-admin
    stage-close
} -returnCodes error -result [not-authorized]

test create-org-1.4.1 {create-org, non-admin login is not ok} -constraints s342ge -setup {
    stage-open
    go-admin
    go-non-admin
} -body {
    run create-org [theorg]-2
} -cleanup {
    be-admin
    remove-non-admin
    stage-close
} -returnCodes error -result {Error 330002: Feature Disabled: user_org_creation (403)}

test create-org-1.5 {create-org, persists} -constraints cfv2 -setup {
    stage-open
    go-admin
} -body {
    run create-org [theorg]-2
    run orgs
} -cleanup {
    run delete-org -n [theorg]-2
    stage-close
} -match glob -result "*| x | [theorg]-2 *|*"

test create-org-1.6 {create-org, duplicate org} -constraints cfv2 -setup {
    stage-open
    go-admin
    run create-org [theorg]-2
} -body {
    run create-org [theorg]-2
} -cleanup {
    run delete-org -n [theorg]-2
    stage-close
} -returnCodes error -result [already input name organization [theorg]-2]

test create-org-1.7.0 {create-org} -constraints cfv2 -setup {
    stage-open
    go-admin
} -body {
    run create-org [theorg]-2
} -cleanup {
    run delete-org -n [theorg]-2
    stage-close
} -result [map [viewFile data/corg17.txt] \
	       @org@    [theorg]-2 \
	       @target@ [thetarget]]

test create-org-1.7.1 {create-org, unicode name} -constraints {macOrUnix cfv2} -setup {
    stage-open
    go-admin
} -body {
    run create-org [theorg]\u4e00
} -cleanup {
    run delete-org -n [theorg]\u4e00
    stage-close
} -result [map [viewFile data/corg17.txt] \
	       @org@    [theorg]\u4e00 \
	       @target@ [thetarget]]

# # ## ### ##### ######## ############# #####################
cleanupTests
