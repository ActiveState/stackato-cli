# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2014 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## unbind-security-group

kt check Tcl     8.5
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt source support/common.tcl

# # ## ### ##### ######## ############# #####################

test unbind-security-group-1.0.1 {unbind-security-group, wrong args, not enough} -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run unbind-security-group -n
} -cleanup {
    stage-close
} -returnCodes error -match glob -result "wrong#args, not enough*"

test unbind-security-group-1.0.2 {unbind-security-group, wrong args, too many} -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run unbind-security-group S X
} -cleanup {
    remove-appdir
    stage-close
} -returnCodes error -match glob -result "wrong#args, too many*"

test unbind-security-group-1.0.3 {unbind-security-group, bad option} -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run unbind-security-group -n S --bogus
} -cleanup {
    stage-close
} -returnCodes error -match glob -result "Unknown option --bogus*"

test unbind-security-group-1.0.4.0 {unbind-security-group, option conflict} -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run unbind-security-group -n S --staging --running
} -cleanup {
    stage-close
} -returnCodes error -result {You cannot use "--running" together with "--staging".}

test unbind-security-group-1.0.4.1 {unbind-security-group, option conflict vice versa} -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run unbind-security-group -n S --running --staging
} -cleanup {
    stage-close
} -returnCodes error -result {You cannot use "--staging" together with "--running".}


test unbind-security-group-1.1.0 {unbind-security-group, login required} -setup {
    stage-open
    ref-target
} -body {
    run unbind-security-group [thesecgroup]
} -cleanup {
    stage-close
} -returnCodes error -result [login-required]

test unbind-security-group-1.1.1 {unbind-security-group, non-admin login is not ok} -constraints s342ge -setup {
    stage-open
    go-admin
    run create-security-group [thesecgroup] [tmp]/data/sg/0-rules.json
    go-non-admin
} -body {
    run unbind-security-group -n [thesecgroup]
} -cleanup {
    be-admin
    run delete-security-group -n [thesecgroup]
    remove-non-admin
    stage-close
} -returnCodes error \
    -result [unexpected input security_group {security group} [thesecgroup] {Run 'stackato security-groups' to see list of security groups}]
# Instead of a [not-authorized] return the sgroups are simply hidden from the user.

# # ## ### ##### ######## ############# #####################

test unbind-security-group-1.3 {unbind-security-group, persists} -constraints s342ge -setup {
    stage-open
    go-admin
    run create-security-group [thesecgroup] [tmp]/data/sg/0-rules.json
    run bind-security-group [thesecgroup] --org [theorg] --space [thespace]
} -body {
    run unbind-security-group [thesecgroup] --org [theorg] --space [thespace]
    run security-groups
} -cleanup {
    run delete-security-group -n [thesecgroup]
    stage-close
} -match glob -result "*\n| Name *| \#Rules *| Spaces *| Staging *| Running *|\n*| [thesecgroup] *| 0 *|   *| 0 *| 0 *|*"

test unbind-security-group-1.4 {unbind-security-group, unknown security-group} -constraints s342ge -setup {
    stage-open
    go-admin
} -body {
    run unbind-security-group [thesecgroup]
} -cleanup {
    stage-close
} -returnCodes error \
    -result [unexpected input security_group {security group} [thesecgroup] {Run 'stackato security-groups' to see list of security groups}]

test unbind-security-group-1.5.0 {unbind-security-group, space} -constraints s342ge -setup {
    stage-open
    go-admin
    run create-security-group [thesecgroup] [tmp]/data/sg/0-rules.json
    run bind-security-group [thesecgroup] --org [theorg] --space [thespace]
} -body {
    run unbind-security-group [thesecgroup] --org [theorg] --space [thespace]
} -cleanup {
    run delete-security-group -n [thesecgroup]
    stage-close
} -result [map [viewFile data/sg/unbind.txt] \
	       @or@ [theorg] \
	       @sp@ [thespace] \
	       @sg@ [thesecgroup]]

test unbind-security-group-1.5.1 {unbind-security-group, staging} -constraints s342ge -setup {
    stage-open
    go-admin
    run create-security-group [thesecgroup] [tmp]/data/sg/0-rules.json
    run bind-security-group [thesecgroup] --staging
} -body {
    run unbind-security-group [thesecgroup] --staging
} -cleanup {
    run delete-security-group -n [thesecgroup]
    stage-close
} -result [map [viewFile data/sg/unbind-staging.txt] \
	       @sg@ [thesecgroup]]

test unbind-security-group-1.5.2 {unbind-security-group, running} -constraints s342ge -setup {
    stage-open
    go-admin
    run create-security-group [thesecgroup] [tmp]/data/sg/0-rules.json
    run bind-security-group [thesecgroup] --running
} -body {
    run unbind-security-group [thesecgroup] --running
} -cleanup {
    run delete-security-group -n [thesecgroup]
    stage-close
} -result [map [viewFile data/sg/unbind-running.txt] \
	       @sg@ [thesecgroup]]

# # ## ### ##### ######## ############# #####################
cleanupTests
