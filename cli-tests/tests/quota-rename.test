# -*- tcl -*- tcl.tk//DSL tcltest//EN//2.0 tcl.tk//DSL tcltest//EN//2.0
## (c) 2014 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## quota rename

kt check Tcl     8.5
kt check tcltest 2

# # ## ### ##### ######## ############# #####################

kt source support/common.tcl

# # ## ### ##### ######## ############# #####################

test quota-rename-1.0 {quota rename, wrong args, too many} -setup {
    stage-open
} -body {
    run quota rename Q NQ X
} -cleanup {
    stage-close
} -returnCodes error -match glob -result "wrong#args, too many*"

test quota-rename-1.1 {quota rename, wrong args, not enough} -constraints cfv2 -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run quota rename -n
} -cleanup {
    stage-close
} -returnCodes error -match glob -result "wrong#args, not enough*"

test quota-rename-1.2 {quota rename, wrong args, not enough} -constraints cfv2 -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run quota rename -n Q
} -cleanup {
    stage-close
} -returnCodes error -result {Missing definition for argument 'name'}

test quota-rename-1.3 {quota requires CFv2 API} -constraints cfv1 -setup {
    stage-open
    ref-target
    go-admin
} -body {
    run quota rename -n Q NQ
} -cleanup {
    stage-close
} -returnCodes error -result {Error: This command requires a target exporting the CF v2 API}

test quota-rename-1.4 {quota rename, not logged in} -constraints cfv2 -setup {
    stage-open
    ref-target
} -body {
    run quota rename testquota testquota2
} -cleanup {
    stage-close
} -returnCodes error -result [login-required]

test quota-rename-1.5 {quota rename, non-admin login bad} -constraints cfv2 -setup {
    stage-open
    ref-target
    go-admin
    run quota create testquota
    go-non-admin
} -body {
    run quota rename -n testquota testquota2
} -cleanup {
    go-admin
    run quota delete -n testquota
    remove-non-admin
    stage-close
} -returnCodes error -result [not-authorized]

test quota-rename-1.6 {quota rename, unknown source quota} -constraints cfv2 -setup {
    stage-open
    go-admin
} -body {
    run quota rename -n testquota testquota2
} -cleanup {
    stage-close
} -returnCodes error \
    -result [unexpected input name {quota plan} testquota {Run 'stackato quotas' to see list of quota plans}]

test quota-rename-1.7 {quota rename, duplicate destination quota} -constraints cfv2 -setup {
    stage-open
    go-admin
    run quota create testquota
    run quota create testquota2
} -body {
    run quota rename -n testquota testquota2
} -cleanup {
    run quota delete -n testquota2
    run quota delete -n testquota
    stage-close
} -returnCodes error -result [already input newname {quota plan} testquota2]

test quota-rename-1.8 {quota rename} -constraints cfv2 -setup {
    stage-open
    go-admin
    run quota create testquota
} -body {
    run quota rename -n testquota testquota2
} -cleanup {
    run quota delete -n testquota2
    stage-close
} -result {Renaming quota plan to testquota2 ... OK}

test quota-rename-1.9 {quota rename, persists} -constraints cfv2 -setup {
    stage-open
    go-admin
    run quota create testquota
} -body {
    run quota rename -n testquota testquota2
    run quota show testquota2
} -cleanup {
    run quota delete -n testquota2
    stage-close
} -match glob -result *

# # ## ### ##### ######## ############# #####################
cleanupTests
